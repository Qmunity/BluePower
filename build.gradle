plugins {
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.parchmentmc.librarian.forgegradle'

project.ext {
    major = mod_version_major
    minor = mod_version_minor
    if (System.getenv("CI_PIPELINE_IID") != null) {
        build = System.getenv("CI_PIPELINE_IID") + "-alpha"
    } else {
        build = "x-custom"
    }
    mcVersion = minecraft_version
}

version = "${project.ext.major}.${project.minor}.${project.build}"
group = "com.bluepowermod"
archivesBaseName = "bluepower"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    maven {
        // location of the maven that hosts Jade/WAILA files
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Modmaven"
        url = "https://modmaven.dev"
    }
}

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                bluepower {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', 'bluepower'
        }

        server {
            property 'forge.enabledGameTestNamespaces', 'bluepower'
            args '--nogui'
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', 'bluepower', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"
    compileOnly fg.deobf("curse.maven:${jade_version}")
    compileOnly fg.deobf("mezz.jei:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:${jei_version}")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version   : minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version         : neo_version, neo_version_range: neo_version_range,
            loader_version_range: loader_version_range, mod_version: version,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes(["Specification-Title": "bluepower",
                    "Specification-Vendor": "qmunity",
                    "Specification-Version": "28.0",
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.jar.archiveVersion,
                    "Implementation-Vendor" :"qmunity",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
    archiveAppendix = "${project.ext.mcVersion}"
    archiveClassifier = "universal"
    finalizedBy 'reobfJar'
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output) {
        archiveAppendix = "${project.ext.mcVersion}"
        archiveClassifier = "deobf"
    }
}

// Create API library zip
task apiZip(type: Zip) {
    from(sourceSets.main.java) {
        include 'com/bluepowermod/api/**'
        archiveAppendix = "${project.ext.mcVersion}"
        archiveClassifier = "api"
    }
}

apiZip.mustRunAfter deobfJar

artifacts {
    archives deobfJar
    archives apiZip
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}